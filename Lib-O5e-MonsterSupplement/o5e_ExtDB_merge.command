[h: o5eResponse = arg (0)]
[h: url = arg (1)]
[h: urlTokens = stringToList (url, "=")]
[h, if (listCount (urlTokens) > 1): slug = decode (listGet (urlTokens, 1)); assert (0, url + " is an invalid search token")]
[h: o5e_ExtDB_Constants (getMacroName())]
[h: log.debug (CATEGORY + "slug = " + slug)]

[h: responseCount = json.get (o5eResponse, "count")]
[h, if (responseCount == ""): responseCount = 0]
[h: responseResults = json.get (o5eResponse, "results")]
[h: extDb = o5e_ExtDB_getDB()]
[h: extDbResults = json.path.read (extDb, ".[?(@.monsterDBName =~ /.*" + slug + ".*/i)]")]
[h: log.debug (CATEGORY + "extDbResults = " + extDbResults)]
[h, if (!json.isEmpty (extDbResults)), code: {
	[extDbResultsCount = json.length (extDbResults)]
	[responseCount = responseCount + extDbResultsCount]
	[responseResults = json.merge (responseResults, extDbResults)]
	[log.debug (CATEGORY + "merged responseResults = " + responseResults)]
	[sortedResults = dnd5e_Util_JSON_sortObjectArray (responseResults, "slug")]
	[log.debug (CATEGORY + "sortedResults = " + sortedResults)]
	[o5eResponse = json.set (o5eResponse, "count", responseCount,
			"results", sortedResults)]
}]
[h: macro.return = o5eResponse]