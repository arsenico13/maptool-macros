[h: rollExpressions = arg (0)]
[h: attackIndex = arg (1)]
[h: dnd5e_Constants (getMacroName())]
[h: attackExpression = "{}"]
[h: primaryDamageExpression = "{}"]
[h: extraDamageExpressions = "[]"]

[h: foundPrimaryDamage = 0]
[h, foreach (rollExpression, rollExpressions, ""), code: {
	[if (dnd5e_RollExpression_hasType (rollExpression, "Attack")): attackExpression = rollExpression; ""]
	[if (dnd5e_RollExpression_hasType (rollExpression, "Damage") && foundPrimaryDamage): 
			extraDamageExpressions = json.append (extraDamageExpressions, rollExpression); ""]
	[if (dnd5e_RollExpression_hasType (rollExpression, "Damage") && !foundPrimaryDamage), code: {
		[foundPrimaryDamage = 1]
		[primaryDamageExpression = rollExpression]
	}; {""}]
	[if (dnd5e_RollExpression_hasType (rollExpression, "saveable")):
			extraDamageExpressions = json.append (extraDamageExpressions, rollExpression); ""]
}]
[h: isGenerated = dnd5e_RollExpression_getTypedDescriptor (attackExpression, TYPED_DESCRIPTORS.GENERATED)]
[h: log.debug (CATEGORY + "##isGenerated = " + isGenerated)]
[h, if (!isNumber (isGenerated)): isGenerated = 0]
[h: generatedSrc = "unknown"]
[h, if (isGenerated): tmpGeneratedSrc = dnd5e_RollExpression_getTypedDescriptor (attackExpression, TYPED_DESCRIPTORS.GENERATED_SOURCE); 
	tmpGeneratedSrc = ""]
[h, if (tmpGeneratedSrc != ""): generatedSrc = tmpGeneratedSrc]

[h: attackName = dnd5e_RollExpression_getName (attackExpression)]
<div class="attack-container">
	[r: dnd5e_Editor_getAttackDamageHtml (attackExpression, primaryDamageExpression, attackIndex)]
  <hr />
    [r: dnd5e_Editor_getAdditionalDamageHtml (extraDamageExpressions, attackIndex)]
</div>
    [r, if (isGenerated), code: {
	<div>
		<label class="warning-label">* This attack is generated by [r: generatedSrc] and should not be directly modified.</label>
	</div>
    }]
