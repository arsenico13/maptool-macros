[h: forCounterField = arg(0)]
[h: dnd5e_CharSheet_Constants (getMacroName())]
[h: counters = getProperty (PROP_COUNTERS)]
[h: log.debug (CATEGORY + "## counters = " + counters)]
[h, if (json.type (counters) != "OBJECT"): counters = "{}"]
<!-- Gonna go crazy and use string props! -->
[h: typeArry = json.append ("", "rounds", "minutes", "hours", "other")]
[h: counterTabStr = "%{counterField} | %{counterName} || TAB"]
[h: counterNameStr = "name | %{counterName} | Counter Name | TEXT | width=8"]
[h: counterCurrentStr = "current | %{counterCurrent} | Current Value | TEXT | width=5"]
[h: counterLimitStr = "limit | %{counterLimit} | Expires After | TEXT | width=5"]
[h: counterTypeStr = "type | " + typeArry + " | The type | LIST | delimiter=json select=%{typeSelect} value=string span=true"]
[h: counterDeleteStr = "delete | 0 | Delete Counter | CHECK"]

[h: inputStr = ""]
[h: counterFields = json.fields (counters, "json")]
[h: newCounterField = "NEW_COUNTER"]
[h: counterFields = json.append (counterFields, newCounterField)]
[h, if (forCounterField != ""): counterFields = json.append ("", forCounterField)]
[h: counters = json.set (counters, newCounterField, "limit=10 ## type=rounds ##")]
[h: log.debug (CATEGORY + "## counterFields = " + counterFields)]
[h: tabFields = "[]"]
[h, foreach (counter, counterFields), code: {
	[counterStrProp = json.get (counters, counter)]
	[counterName = getStrProp (counterStrProp, "name", counter, "##")]
	[counterField = dnd5e_CharSheet_Util_getTabSafeVar (counterName)]
	[tabFields = json.append (tabFields, counterField)]
	[counterLimit = getStrProp (counterStrProp, "limit", 10, "##")]
	[counterCurrent = getStrProp (counterStrProp, "current", counterLimit, "##")]
	[typeStr = getStrProp (counterStrProp, "type", "rounds", "##")]
	[typeSelect = json.indexOf (typeArry, typeStr)]
	[if (typeSelect < 0): typeSelect = 0]
	[log.debug (CATEGORY + "## counterStrProp = " + counterStrProp + "; counterCurrent = " + 
			counterCurrent + "; counterLimit = " + counterLimit + "; typeStr = " + typeStr +
			"; typeSelect = " + typeSelect)]
	[inputStr = listAppend (inputStr, strformat (counterTabStr), "##")]
	[inputStr = listAppend (inputStr, strformat (counterNameStr), "##")]
	[inputStr = listAppend (inputStr, strformat (counterCurrentStr), "##")]
	[inputStr = listAppend (inputStr, strformat (counterLimitStr), "##")]
	[inputStr = listAppend (inputStr, strformat (counterTypeStr), "##")]
	[inputStr = listAppend (inputStr, counterDeleteStr, "##")]
}]
[h: retObj = json.set ("", "inputString", inputStr,
	"tabFields", tabFields)]
[h: log.trace (CATEGORY + "## retObj = " + retObj)]
[h: macro.return = retObj]